#Область ПрограммныйИнтерфейс

Процедура ОтправкаУведомленийВТелеграм() Экспорт 

	Токен = Константы.ВКМ_ТокенУправленияТГБотом.Получить();
	Идентификатор = Константы.ВКМ_ИдентификаторГруппыДляОповощения.Получить();
	
	Если Не ЗначениеЗаполнено(Токен) Тогда
		
		ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации("Отправка сообщений в телеграм. Не отправлено. Ошибка",
			УровеньЖурналаРегистрации.Ошибка,,, "Токен не установлен");
			
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		
		ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации("Отправка сообщений в телеграм. Не отправлено. Ошибка",
			УровеньЖурналаРегистрации.Ошибка,,, "Идентификатор не установлен");
			
	КонецЕсли;
	
	Уведомления = УведомленияДляОтправки();
	
	Пока Уведомления.Следующий() Цикл
		
		Ответ = ОтправитьСообщение(Токен, Идентификатор, Уведомления.ТекстСообщения);
		
		Если Ответ.КодСостояния = 200 Тогда
			
			Уведомление = Уведомления.Ссылка.ПолучитьОбъект();
			Уведомление.Удалить();
			
		Иначе 
			
			Ошибка = Ответ.ПолучитьТелоКакСтроку();
			ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации("Отправка сообщений в телеграм. Не отправлено. Ошибка",
				УровеньЖурналаРегистрации.Ошибка,,, Ошибка);
		
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
Функция УведомленияДляОтправки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_УведомленияВТелеграм.Ссылка,
		|	ВКМ_УведомленияВТелеграм.ТекстСообщения
		|ИЗ
		|	Справочник.ВКМ_УведомленияВТелеграм КАК ВКМ_УведомленияВТелеграм";
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция ОтправитьСообщение(Токен, Идентификатор, Текст)
	
	Попытка 
		
		Соединение = Новый HTTPСоединение("api.telegram.org",,,,,, ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение());
		
		СтруктураСообщения = Новый Структура("chat_id, text", Идентификатор, Текст);
		
		ЗаголовокЗапросаHTTP = Новый Соответствие();
		ЗаголовокЗапросаHTTP.Вставить("Content-Type", "application/json");
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON, СтруктураСообщения);
		Сообщение = ЗаписьJSON.Закрыть();
		
		URL = СтрШаблон("/bot%1/sendMessage", Токен);
		HTTPЗапрос = Новый HTTPЗапрос(URL, ЗаголовокЗапросаHTTP);
		HTTPЗапрос.УстановитьТелоИзСтроки(Сообщение);
		Ответ = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
		
	Исключение
		
		ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации("Отправка сообщений в телеграм. Не отправлено. Ошибка",
			УровеньЖурналаРегистрации.Ошибка,,,ОписаниеОшибки());
		Ответ = Неопределено;
		
	КонецПопытки;
	
	Возврат Ответ;
		
КонецФункции

#КонецОбласти
